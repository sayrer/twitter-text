// Copyright 2025 Robert Sayre
// Licensed under the Apache License, Version 2.0
// http://www.apache.org/licenses/LICENSE-2.0

//
// A tweet is composed of a sequence of text and entities
//
tweet = { SOI ~ (entity | invalid_char | text)+ ~ EOI }

invalid_char = {
    "\u{fffe}" | // BOM
    "\u{feff}" | // BOM
    "\u{ffff}"   // Special
}

text = _{ !invalid_char ~ (emoji | ANY) }

//
// An "entity" represents some special content embedded in the
// tweet. This grammar only covers those that have representations
// in the tweet text. There are other types, such as polls, that do not appear in the text.
//
// Pest uses a Parsing Expression Grammar (PEG), so the order of the choices here
// is significant <https://en.wikipedia.org/wiki/Parsing_expression_grammar#Semantics>.
// In particular, placing URLs before other entities helps resolve ambiguities where
// URL fragments might also be interpreted as hashtags (see <https://github.com/twitter/twitter-text/blob/30e2430d90cff3b46393ea54caf511441983c260/rb/lib/twitter-text/extractor.rb#L65>).
//
entity = _{ possible_url | possible_hashtag | possible_federated_mention | possible_mention | possible_cashtag }

//
// CASHTAGS
//
cashtag_prefix = { "$" }
cashtag_text = _{ ASCII_ALPHA{1,6} ~ (("." | "_")? ~ ASCII_ALPHA{1,2})? ~ !(ASCII_DIGIT | ASCII_ALPHA) }
cashtag_token = _{ cashtag_prefix ~ cashtag_text }
cashtag = { cashtag_token }
invalid_cashtag_start = _{ (!space ~ text) ~ cashtag_token | cashtag_prefix ~ protocol ~ subdomain_char* }
possible_cashtag = _{ invalid_cashtag_start | (cashtag ~ cashtag_token*) }


//
// HASHTAGS
//
hashtag_prefix = _{ "#" | "ÔºÉ" }
hashtag_letter_or_mark = _{
    UPPERCASE_LETTER | LOWERCASE_LETTER | TITLECASE_LETTER | MODIFIER_LETTER |
    OTHER_LETTER | NONSPACING_MARK | ENCLOSING_MARK | SPACING_MARK
}
hashtag_special = _{
    DECIMAL_NUMBER | "_" |
    "\u{200c}" | "\u{200d}" | "\u{a67e}" | "\u{05be}" | "\u{05f3}" | "\u{05f4}" | "\u{ff5e}" | "\u{301c}" |
    "\u{309b}" | "\u{309c}" | "\u{30a0}" | "\u{30fb}" | "\u{3003}" | "\u{0f0b}" | "\u{0f0c}" | "\u{00b7}"
}
hashtag_text = _{ !("\u{fe0f}" | "\u{20e3}") ~ (hashtag_special*  ~ hashtag_letter_or_mark+ ~ hashtag_special*)+ }
hashtag = { hashtag_prefix ~ hashtag_text }
invalid_hashtag_start = _{
    (("&" | hashtag_letter_or_mark) ~ hashtag_prefix ~ &hashtag_text) |
    hashtag_prefix ~ protocol ~ subdomain_char*
}
possible_hashtag = _{ "\u{fe0e}" | "\u{fe0f}" ~ hashtag | invalid_hashtag_start | hashtag }


//
// MENTIONS
//
possible_mention = _{ list | user }

//
// USER
//
user_prefix = _{ "@" | "Ôº†" }
user_char = _{ alphanumeric | "_" }
user_token = _{ user_prefix ~ user_char{1, 20} }
user_invalid_prefix = _{ user_char | "!" | "#" | "$" | "%" | "&" | "*" | user_prefix | protocol }
user_invalid_suffix = _{ user_prefix | latin_accent | "://" | "-" }
rt_prefix = _{ !(user_char | "+" | "~" | "." | "-") | SOI ~ ^"rt" ~ ":"? }
username = { user_token }
user = _{
    user_prefix ~ protocol ~ subdomain_char* |
    user_token ~ user_token+ | // ignore @text@like@this
    user_token ~ user_invalid_suffix |
    rt_prefix ~ username |
    user_invalid_prefix ~ user_token |
    username
}

//
// FEDERATED MENTION (Mastodon-style @user@domain.tld)
//
// Username part: alphanumeric and underscore, with optional .-separated segments
federated_username_char = _{ ASCII_ALPHANUMERIC | "_" }
federated_username_segment = _{ federated_username_char+ }
federated_username = _{ federated_username_segment ~ (("." | "-")+ ~ federated_username_segment)* }
// Domain part: word characters with optional .-separated segments
federated_domain_char = _{ ASCII_ALPHANUMERIC | "_" }
federated_domain_segment = _{ federated_domain_char+ }
federated_domain = _{ federated_domain_segment ~ (("." | "-")+ ~ federated_domain_segment)* }
// Full federated mention: @username@domain (must not be preceded by word char, =, or /)
federated_mention_invalid_prefix = _{ ASCII_ALPHANUMERIC | "_" | "=" | "/" }
federated_mention = { user_prefix ~ federated_username ~ "@" ~ federated_domain }
valid_federated_mention = { SOI ~ federated_mention ~ EOI }
// Possible federated mention: handles invalid prefix cases
possible_federated_mention = _{ federated_mention_invalid_prefix ~ federated_mention | federated_mention }

//
// LIST
//
listname = { user_token }
list_slug = { "/" ~ (ASCII_ALPHA ~ (alphanumeric | "_" | "-"){,24}){1} }
list = { listname ~ list_slug }

//
// REPLY
//
reply = _{ space? ~ username ~ !user_invalid_suffix }


//
// URLs beginning with "http"
//
// Early rejection: only attempt URL parsing if there's a "." before the next space/EOF.
// This avoids expensive TLD matching at positions that can't possibly be URLs.
possible_url = _{ !(user_prefix | hashtag_prefix | cashtag_prefix) ~ &url_has_dot ~ (url | uwp) }
url_has_dot = _{ (!space ~ !"." ~ ANY)* ~ "." }
url = { tco_url | normal_url }
tco_url = _{
    (protocol ~ tco_domain ~ "/" ~ alphanumeric{1,40} ~ (query ~ fragment?)? ~ !(alphanumeric | cyrillic_char | latin_accent)) |
     protocol ~ tco_domain ~ "/"? ~ !(alphanumeric | cyrillic_char | latin_accent)
}
tco_domain =  { "t.co" }
normal_url = _{ protocol ~ userinfo? ~ host ~ port? ~ path? ~ query? ~ fragment? }

//
// PROTOCOL
//
protocol = _{ ^"http" ~ (^"s")? ~ "://" }

//
// USERINFO
//
pct_encoded = _{ "%" ~ HEX_DIGIT }
sub_delims = _{ "!" | "$" | "&" | "'" | "(" | ")" | "*" | "+" | "," | ";" | "=" }
unreserved = _{ alphanumeric | "-" | "." | "_" | "~" | cyrillic_char }
userinfo = _{ (unreserved | pct_encoded | sub_delims | ":")* ~ "@" }

//
// DOMAINS
//
host = { !"t.co" ~ domain | ipv4_address | ip_literal }

//
// TLD
//
invalid_tld_suffix = _{ 'a'..'z' | '0'..'9' | "@" | "-" }
tld = { tld_list ~ !invalid_tld_suffix }

//
// TEXT DOMAINS
//
domain = { (sl_domain | longer_domain) ~ tld }
sl_domain = _{
    (domain_segment ~ "." ~ &tld) ~
    !((domain_segment ~ "." ~ &tld) | "." ~ &tld | longer_domain)
}
longer_domain = _{ subdomain ~ ("." ~ subdomain)* ~ "." ~ sl_domain }
subdomain = _{ !sl_domain ~ subdomain_segment }
punycode_host = _{ ^"xn" ~ "--" ~ ("-" | alphanumeric)+ }
domain_segment = _{punycode_host | (domain_char ~ ("-"? ~ domain_char)*)+}
domain_char = _{ (non_punctuation ~ ("-"? ~ non_punctuation)+) | non_punctuation }
subdomain_segment = _{punycode_host | subdomain_char+}
subdomain_char = _{ (domain_char ~ "_"? ~ domain_char) | domain_char }

//
// IPv4 ADDRESS
// Adapted from core-pegjs <https://goo.gl/LQPPAi>
//
ipv4_address = _{ dec_octet ~ "." ~ dec_octet ~ "." ~ dec_octet ~ "." ~ dec_octet }
dec_octet = _{
    "25" ~ '\u{0030}'..'\u{0035}' |            // 250-255
    "2" ~ '\u{0030}'..'\u{0034}' ~ '0'..'9' |  // 200-249
    "1" ~ '0'..'9' ~ '0'..'9' |                // 100-199
    '\u{0031}'..'\u{0039}' ~ '0'..'9' |        // 10-99
    '0'..'9'                                   // 0-9
}

//
// IPv6 ADDRESS
// Adapted from core-pegjs <https://goo.gl/LQPPAi>
//
ip_literal = _{ "[" ~ ipv6_address ~ "]" }
ipv6_address = _{
    (                   h16_{6} ~ ls32) |
    (            "::" ~ h16_{5} ~ ls32) |
    (h16_{0,1} ~ "::" ~ h16_{4} ~ ls32) |
    (h16_{0,2} ~ "::" ~ h16_{3} ~ ls32) |
    (h16_{0,3} ~ "::" ~ h16_{2} ~ ls32) |
    (h16_{0,4} ~ "::" ~ h16_{1} ~ ls32) |
    (h16_{0,5} ~ "::" ~           ls32) |
    (h16_{0,6} ~ "::" ~           h16 ) |
    (h16_{0,7} ~ "::"                 )
}
ls32 = _{ (h16 ~ ":" ~ h16) | ipv4_address }
h16_ = _{ h16 ~ ":" }
h16 = _{ HEX_DIGIT{1,4} }

//
// URL WITHOUT PROTOCOL (UWP)
//
invalid_preceding_url_chars = { "_" | "-" | "." | protocol }
uwp = { invalid_preceding_url_chars ~ uwp_domain | url_without_protocol }
// Email continuation pattern: -something@ or +something@ (don't extract URL if followed by this)
email_local_char = _{ alphanumeric | "." | "_" | "-" | "+" }
email_continuation = _{ ("-" | "+") ~ email_local_char* ~ ("@" | "\u{ff20}") }
url_without_protocol = { uwp_domain ~ !email_continuation ~ port? ~ path? ~ query? ~ fragment? }
// Short-circuit: require a dot before space/EOF to avoid expensive TLD hunting
uwp_domain = { &((!space ~ !EOI ~ !"." ~ ANY)* ~ ".") ~ (uwp_sl_domain | uwp_longer_domain) ~ tld }
uwp_sl_domain = _{ (uwp_domain_char+ ~ "." ~ &tld) ~ !((uwp_domain_char+ ~ "." ~ &tld) | "." ~ &tld | uwp_sl_domain) }
uwp_longer_domain = _{ uwp_subdomain ~ ("." ~ uwp_subdomain)* ~ "." ~ uwp_sl_domain }
uwp_subdomain = { !uwp_sl_domain ~ uwp_subdomain_char+ }
uwp_subdomain_char = _{ (uwp_domain_char ~ "_"? ~ uwp_domain_char) | uwp_domain_char }
uwp_domain_char = _{
    ((alphanumeric | latin_accent) ~ ("-"? ~ (alphanumeric | latin_accent))+)
    | alphanumeric | latin_accent
}

//
// PORT
//
port = { ":" ~ '1'..'9' ~ ASCII_DIGIT* }

//
// PATH
//
url_general_path = _{ url_path_end | path_punctuation }
url_path_end = _{ alphanumeric | "=" | "_" | "-" | "+" | cyrillic_char | latin_accent | url_balanced_parens }
path_punctuation = _{
   "!" | "*" | "\'" | ";" | ":" | "," | "." | "$" | "%" | "[" | "]" | "\u{2013}" | "~" | "|" | "&" | "@"
}

// Allow URL paths to contain up to two nested levels of balanced parens
//  1. Used in Wikipedia URLs like /Primer_(film)
//  2. Used in IIS sessions like /S(dfd346)/
//  3. Used in Rdio URLs like /track/We_Up_(Album_Version_(Edited))/
balanced_paren_path_text = _{ "(" ~ url_general_path+ ~ ")" }
url_balanced_parens = _{
    balanced_paren_path_text |
    "(" ~ url_general_path* ~ balanced_paren_path_text ~ url_general_path* ~ ")"
}

end_path = _{ (url_path_end | (path_punctuation+ ~ &url_path_end))+ }
segment = _{ (url_general_path+ ~ &"/") | end_path }
path = _{ "/" ~ ((segment ~ ("/" ~ segment)*) ~ ("/")?)?}

//
// QUERY
//
query_punctuation_char = _{
    "!" | "?" | "*" | "\'" | "(" | ")" | ";" | ":" |  "$" | "%" | "[" | "]" | "." | "," | "~" | "|" | "@"
}
query_end_char = _{ ASCII_ALPHA | ASCII_DIGIT | "-" | "_" | "&" | "=" | "/" | "+"}
query_char = _{ query_end_char | query_punctuation_char+ ~ &query_end_char }
query = _{ "?" ~ query_char+ | "?" ~ &"#" }

//
// FRAGMENT
//
fragment_end = _{ query_end_char | "#" }
fragment_char = _{ fragment_end | query_punctuation_char+ ~ &fragment_end }
fragment = _{ "#" ~ fragment_char* }


//
// General rules
//
alphanumeric = _{ ASCII_ALPHA | ASCII_DIGIT }
latin_accent = _{
    '\u{00c0}'..'\u{00d6}' | '\u{00d8}'..'\u{00f6}' | '\u{00f8}'..'\u{00ff}' | '\u{0100}'..'\u{024f}' |
    '\u{0253}'..'\u{0254}' | '\u{0256}'..'\u{0257}' | '\u{0259}'..'\u{0259}' | '\u{025b}'..'\u{025b}' |
    '\u{0263}'..'\u{0263}' | '\u{0268}'..'\u{0268}' | '\u{026f}'..'\u{026f}' | '\u{0272}'..'\u{0272}' |
    '\u{0289}'..'\u{0289}' | '\u{028b}'..'\u{028b}' | '\u{02bb}'..'\u{02bb}' | '\u{0300}'..'\u{036f}' |
    '\u{1e00}'..'\u{1eff}'
}
cyrillic_char = _{ '\u{0400}'..'\u{04ff}' }
space = _{
    "\u{0020}" | "\u{0085}" | "\u{00A0}" | "\u{1680}" | "\u{180E}" | "\u{2028}" | "\u{2029}" |
    "\u{202F}" | "\u{205F}" | "\u{3000}" | '\u{0009}'..'\u{000D}' | '\u{2000}'..'\u{200A}'
}
punctuation_char = _{
    "-" | "_" | "!" | "\"" | "#" | "$" | "%" | "&" | "'" | "(" | ")" | "*" | "+" | "," | "." | "/" |
    "\\" | ":" | ";" | "<" | "=" | ">" | "?" | "@" | "[" | "]" | "^" | "`" | "{" | "|" | "}" | "~"
}
non_punctuation = _{ !(punctuation_char | space | PUNCTUATION) ~ text }

//
// Emoji
//
// Permissive emoji rule for ExternalValidator::External mode.
// Matches potential emoji sequences broadly, then validated externally via emojis crate.
// This is more efficient than the full emoji grammar with all its alternations.
emoji = {
    emoji_start ~ emoji_continuation*
}

// Characters that can start an emoji sequence
emoji_start = _{
    // Emoticons, Dingbats, Symbols
    '\u{00a9}'..'\u{00ae}' |  // ¬© ¬Æ
    '\u{203c}'..'\u{3299}' |  // Various symbols
    '\u{1f000}'..'\u{1faff}' | // Main emoji blocks
    // Regional indicators for flags
    '\u{1f1e0}'..'\u{1f1ff}' |
    // Keycap base characters
    "#" | "*" | '0'..'9'
}

// Characters that can continue an emoji sequence (ZWJ, skin tones, variation selectors, etc.)
emoji_continuation = _{
    // Zero-width joiner followed by more emoji
    ("\u{200d}" ~ emoji_start) |
    // Skin tone modifiers
    '\u{1f3fb}'..'\u{1f3ff}' |
    // Variation selectors
    "\u{fe0e}" | "\u{fe0f}" |
    // Keycap combining mark
    "\u{20e3}" |
    // Regional indicators (for multi-flag sequences)
    '\u{1f1e0}'..'\u{1f1ff}' |
    // Tag characters (for subdivision flags like üè¥Û†ÅßÛ†Å¢Û†Å•Û†ÅÆÛ†ÅßÛ†Åø)
    '\u{e0000}'..'\u{e007f}'
}

// A zero-width-joiner character is used to combine emoji together.
// For example, a face with a skin tone.
zwj = _{ "\u{200d}" }

// A skin tone applied to another emoji
skin_tone = _{ '\u{01f3fb}'..'\u{01f3ff}' }

// A trailing Male or Female symbol
trailing_mf_symbol = _{ zwj ~ (("\u{2640}" | "\u{2642}") ~ "\u{fe0f}"?) }

man_or_woman_with_optional_skin_tone_and_modifier = _{
    // Man, Woman, or Person
    (man_or_woman | "\u{1f9d1}") ~
    // Followed by an optional skin tone
    skin_tone? ~
    // Followed by a joined modifier
    zwj ~
    ((("\u{2695}" | "\u{2696}" | "\u{2708}") ~ "\u{fe0f}"?) |
     ("\u{1f33e}" | "\u{1f373}" | "\u{1f37c}" | "\u{1f384}" | "\u{1f393}" | "\u{1f3a4}" | "\u{1f3a8}" |
      "\u{1f3eb}" | "\u{1f3ed}" | "\u{1f4bb}" | "\u{1f4bc}" | "\u{1f527}" | "\u{1f52c}" | "\u{1f680}" |
      "\u{1f692}" | '\u{1f9b0}'..'\u{1f9b3}' | "\u{1f9af}" | "\u{1f9bc}" | "\u{1f9bd}" | "\u{1fa70}" |
      "\u{2764}" ~ "\u{fe0f}"? ~ zwj ~ "\u{1f48b}" ~ zwj ~ "\u{1f9d1}")
    )
}

person_with_vs16_or_skin_tone_and_trailing_mf_symbol = _{
    ("\u{26f9}" | "\u{1f3cb}" | "\u{1f3cc}" | "\u{1f574}" | "\u{1f575}") ~
    ("\u{fe0f}" | skin_tone)? ~ trailing_mf_symbol
}

// Couple sequences (kiss, holding hands, couple with heart) with skin tones
// Must be tried before man_or_woman_with_optional_skin_tone_and_modifier
couple_sequences = _{
    // Kiss/couple with heart with skin tones
    (("\u{1f48f}" | "\u{1f491}") ~ skin_tone?) |
    // Person kissing person: person + skin? + ZWJ + heart + fe0f? + ZWJ + kiss + ZWJ + person + skin?
    ((man_or_woman | "\u{1f9d1}") ~ skin_tone? ~ zwj ~ "\u{2764}" ~ "\u{fe0f}"? ~ zwj ~ "\u{1f48b}" ~ zwj ~ (man_or_woman | "\u{1f9d1}") ~ skin_tone?) |
    // Person + heart + person (couple with heart)
    ((man_or_woman | "\u{1f9d1}") ~ skin_tone? ~ zwj ~ "\u{2764}" ~ "\u{fe0f}"? ~ zwj ~ (man_or_woman | "\u{1f9d1}") ~ skin_tone?) |
    // People holding hands
    ((man_or_woman | "\u{1f9d1}") ~ skin_tone? ~ zwj ~ "\u{1f91d}" ~ zwj ~ (man_or_woman | "\u{1f9d1}") ~ skin_tone?) |
    // People with bunny ears (Unicode 17.0): person + skin? + ZWJ + rabbit + ZWJ + person + skin?
    ((man_or_woman | "\u{1f9d1}") ~ skin_tone? ~ zwj ~ "\u{1f430}" ~ zwj ~ (man_or_woman | "\u{1f9d1}") ~ skin_tone?) |
    // People gardening (Unicode 17.0): person + skin? + ZWJ + shovel + ZWJ + person + skin?
    ((man_or_woman | "\u{1f9d1}") ~ skin_tone? ~ zwj ~ "\u{1faef}" ~ zwj ~ (man_or_woman | "\u{1f9d1}") ~ skin_tone?) |
    // Family: person + ZWJ + person + ZWJ + child (+ ZWJ + child)?
    ("\u{1f9d1}" ~ zwj ~ "\u{1f9d1}" ~ zwj ~ "\u{1f9d2}" ~ (zwj ~ "\u{1f9d2}")?) |
    // Parent + child(ren)
    ("\u{1f9d1}" ~ zwj ~ "\u{1f9d2}" ~ (zwj ~ "\u{1f9d2}")?)
}

// Animal/object ZWJ sequences
animal_object_zwj_sequences = _{
    // Service dog
    ("\u{1f415}" ~ zwj ~ "\u{1f9ba}") |
    // Black cat
    ("\u{1f408}" ~ zwj ~ "\u{2b1b}") |
    // Polar bear
    ("\u{1f43b}" ~ zwj ~ "\u{2744}" ~ "\u{fe0f}"?) |
    // Black bird
    ("\u{1f426}" ~ zwj ~ "\u{2b1b}") |
    // Phoenix
    ("\u{1f426}" ~ zwj ~ "\u{1f525}") |
    // Lime
    ("\u{1f34b}" ~ zwj ~ "\u{1f7e9}") |
    // Brown mushroom
    ("\u{1f344}" ~ zwj ~ "\u{1f7eb}") |
    // Broken chain
    ("\u{26d3}" ~ "\u{fe0f}"? ~ zwj ~ "\u{1f4a5}")
}

// Directional person sequences (E15.0+) - person facing direction with optional modifiers
// Must be tried BEFORE person_with_optional_skin_tone_and_trailing_mf_symbol
// Patterns:
//   person + skin_tone? + ZWJ + gender + fe0f? + ZWJ + arrow + fe0f?
//   person + skin_tone? + ZWJ + arrow + fe0f?
//   person + skin_tone? + ZWJ + accessibility + ZWJ + arrow + fe0f?
person_directional_sequences = _{
    // Walking/running/kneeling with optional gender
    (("\u{1f6b6}" | "\u{1f3c3}" | "\u{1f9ce}") ~ skin_tone? ~
     ((zwj ~ ("\u{2640}" | "\u{2642}") ~ "\u{fe0f}"? ~ zwj ~ "\u{27a1}" ~ "\u{fe0f}"?) |
      (zwj ~ "\u{27a1}" ~ "\u{fe0f}"?))) |
    // Person with accessibility device (cane/wheelchair) facing direction
    ((man_or_woman | "\u{1f9d1}") ~ skin_tone? ~ zwj ~
     ("\u{1f9af}" | "\u{1f9bc}" | "\u{1f9bd}") ~ zwj ~ "\u{27a1}" ~ "\u{fe0f}"?)
}

person_with_optional_skin_tone_and_trailing_mf_symbol = _{
    ("\u{1f3c3}" | "\u{1f3c4}" | "\u{1f3ca}" | "\u{1f46e}" | "\u{1f46f}" | "\u{1f470}" | "\u{1f471}" | "\u{1f473}" | "\u{1f477}" |
     "\u{1f481}" | "\u{1f482}" | "\u{1f486}" | "\u{1f487}" | '\u{1f645}'..'\u{1f647}'  | "\u{1f64b}" |
     "\u{1f64d}" | "\u{1f64e}" | "\u{1f6a3}" | '\u{1f6b4}'..'\u{1f6b6}'  | "\u{1f926}" | "\u{1f935}" | "\u{1f936}" |
     '\u{1f937}'..'\u{1f939}'  | "\u{1f93c}" | "\u{1f93d}" | "\u{1f93e}" | "\u{1f9b8}" | "\u{1f9b9}" |
     '\u{1f9cd}'..'\u{1f9cf}' | '\u{1f9d4}'..'\u{1f9dd}') ~ skin_tone? ~ trailing_mf_symbol
}

other_zwj_sequences = _{
    // Starts with Man
    ("\u{1f468}" ~ zwj ~
     (
        ("\u{1f466}" ~ zwj ~ "\u{1f466}") |
        ("\u{1f467}" ~ zwj ~ boy_or_girl) |
        ("\u{1f468}" ~ zwj ~ ("\u{1f466}" ~ zwj ~ "\u{1f466}" | "\u{1f467}" ~ zwj ~ boy_or_girl | boy_or_girl)) |
        ("\u{1f469}" ~ zwj ~ ("\u{1f466}" ~ zwj ~ "\u{1f466}" | "\u{1f467}" ~ zwj ~ boy_or_girl | boy_or_girl)) |
        ("\u{2764}" ~ "\u{fe0f}"? ~ zwj ~ ("\u{1f468}" | "\u{1f48b}" ~ zwj ~ "\u{1f468}")) |
        boy_or_girl
     )
    ) |

    // Starts with Woman
    ("\u{1f469}" ~ zwj ~
     (
        ("\u{1f466}" ~ zwj ~ "\u{1f466}") |
        ("\u{1f467}" ~ zwj ~ boy_or_girl) |
        ("\u{1f469}" ~ zwj ~ ("\u{1f466}" ~ zwj ~ "\u{1f466}" | "\u{1f467}" ~ zwj ~ boy_or_girl | boy_or_girl)) |
        ("\u{2764}" ~ "\u{fe0f}"? ~ zwj ~ (man_or_woman | "\u{1f48b}" ~ zwj ~ man_or_woman)) |
        boy_or_girl
     )
    ) |

    // Zombies, genies, dancers, and wrestlers
    (("\u{1f9df}" | "\u{1f9de}" | "\u{1f46f}" | "\u{1f93c}") ~ trailing_mf_symbol) |

    // Others
    ("\u{1f3f3}" ~ "\u{fe0f}"? ~ zwj ~ ("\u{1f308}" | "\u{26a7}" ~ "\u{fe0f}"?)) |
    ("\u{1f3f4}" ~ zwj ~ "\u{2620}" ~ "\u{fe0f}"?) |
    ("\u{1f441}" ~ "\u{fe0f}"? ~ zwj ~ "\u{1f5e8}" ~ "\u{fe0f}"?) |

    // Face ZWJ sequences (E13.0+)
    ("\u{1f636}" ~ zwj ~ "\u{1f32b}" ~ "\u{fe0f}"?) |  // face in clouds
    ("\u{1f62e}" ~ zwj ~ "\u{1f4a8}") |                 // face exhaling
    ("\u{1f635}" ~ zwj ~ "\u{1f4ab}") |                 // face with spiral eyes
    ("\u{1f642}" ~ zwj ~ ("\u{2194}" | "\u{2195}") ~ "\u{fe0f}"?) |  // head shaking

    // Heart ZWJ sequences (E13.1)
    ("\u{2764}" ~ "\u{fe0f}"? ~ zwj ~ ("\u{1f525}" | "\u{1fa79}")) |

    // Handshake with different skin tones (E14.0) - rightward hand + skin + ZWJ + leftward hand + skin
    ("\u{1faf1}" ~ skin_tone ~ zwj ~ "\u{1faf2}" ~ skin_tone)
}

man_or_woman = _{
    "\u{1f468}" | "\u{1f469}"
}

boy_or_girl = _{
    "\u{1f466}" | "\u{01f467}"
}

// Emojified symbols #, 0, 1, 2, 3, 4, 5, 6, 7, 8, 9 + possible variant selector + keycap
emojified_symbols = _{
    ("#" | "*" | ASCII_DIGIT) ~ "\u{fe0f}"? ~ "\u{20e3}"
}

default_text_symbols = _{
    ("¬©" | "¬Æ" | "\u{2122}" | "\u{265f}" | "\u{26a7}") ~ "\u{fe0f}"?
}

variants_allowing_trailing_fe0f_but_not_fe0e = _{
    ("\u{203c}" | "\u{2049}" | "\u{2139}" | '\u{2194}'..'\u{2199}' | "\u{21a9}" | "\u{21aa}" | "\u{231a}" |
     "\u{231b}" | "\u{2328}" | "\u{23cf}" | '\u{23ed}'..'\u{23ef}' | "\u{23f1}" | "\u{23f2}" | '\u{23f8}'..'\u{23fa}' |
     "\u{24c2}" | "\u{25aa}" | "\u{25ab}" | "\u{25b6}" | "\u{25c0}" | '\u{25fb}'..'\u{25fe}' | '\u{2600}'..'\u{2604}' |
     "\u{260e}" | "\u{2611}" | "\u{2614}" | "\u{2615}" | "\u{2618}" | "\u{2620}" | "\u{2622}" | "\u{2623}" |
     "\u{2626}" | "\u{262a}" | "\u{262e}" | "\u{262f}" | '\u{2638}'..'\u{263a}' | "\u{2640}" | "\u{2642}" |
     '\u{2648}'..'\u{2653}' | "\u{2660}" | "\u{2663}" | "\u{2665}" | "\u{2666}" | "\u{2668}" | "\u{267b}" | "\u{267e}" |
     "\u{267f}" | '\u{2692}'..'\u{2697}' | "\u{2699}" | "\u{269b}" | "\u{269c}" | "\u{26a0}" | "\u{26a1}" |
     "\u{26aa}" | "\u{26ab}" | "\u{26b0}" | "\u{26b1}" | "\u{26bd}" | "\u{26be}" | "\u{26c4}" | "\u{26c5}" |
     "\u{26c8}" | "\u{26cf}" | "\u{26d1}" | "\u{26d3}" | "\u{26d4}" | "\u{26e9}" | "\u{26ea}" |
     '\u{26f0}'..'\u{26f5}' | "\u{26f8}" | "\u{26fa}" | "\u{26fd}" | "\u{2702}" | "\u{2708}" | "\u{2709}" |
     "\u{270f}" | "\u{2712}" | "\u{2714}" | "\u{2716}" | "\u{271d}" | "\u{2721}" | "\u{2733}" | "\u{2734}" |
     "\u{2744}" | "\u{2747}" | "\u{2757}" | "\u{2763}" | "\u{2764}" | "\u{27a1}" | "\u{2934}" | "\u{2935}" |
     '\u{2b05}'..'\u{2b07}' | "\u{2b1b}" | "\u{2b1c}" | "\u{2b50}" | "\u{2b55}" | "\u{3030}" | "\u{303d}" |
     "\u{3297}" | "\u{3299}" | "\u{1f004}" | "\u{1f170}" | "\u{1f171}" | "\u{1f17e}" | "\u{1f17f}" | "\u{1f202}" |
     "\u{1f21a}" | "\u{1f22f}" | "\u{1f237}" | "\u{1f321}" | '\u{1f324}'..'\u{1f32c}' | "\u{1f336}" | "\u{1f37d}" |
     "\u{1f396}" | "\u{1f397}" | '\u{1f399}'..'\u{1f39b}' | "\u{1f39e}" | "\u{1f39f}" | "\u{1f3cd}" | "\u{1f3ce}" |
     '\u{1f3d4}'..'\u{1f3df}' | "\u{1f3f3}" | "\u{1f3f5}" | "\u{1f3f7}" | "\u{1f43f}" | "\u{1f441}" | "\u{1f4fd}" |
     "\u{1f549}" | "\u{1f54a}" | "\u{1f56f}" | "\u{1f570}" | "\u{1f573}" | '\u{1f576}'..'\u{1f579}' | "\u{1f587}" |
     '\u{1f58a}'..'\u{1f58d}' | "\u{1f5a5}" | "\u{1f5a8}" | "\u{1f5b1}" | "\u{1f5b2}" | "\u{1f5bc}" |
     '\u{1f5c2}'..'\u{1f5c4}' | '\u{1f5d1}'..'\u{1f5d3}' | '\u{1f5dc}'..'\u{1f5de}' | "\u{1f5e1}" | "\u{1f5e3}" |
     "\u{1f5e8}" | "\u{1f5ef}" | "\u{1f5f3}" | "\u{1f5fa}" | "\u{1f6cb}" | '\u{1f6cd}'..'\u{1f6cf}' |
     '\u{1f6e0}'..'\u{1f6e5}' | "\u{1f6e9}" | "\u{1f6f0}" | "\u{1f6f3}") ~
    ("\u{fe0f}" | !"\u{fe0e}")
}

diversity_emoji_with_optional_skin_tone = _{
    ((("\u{261d}" | "\u{26f7}" | "\u{26f9}" | "\u{270c}" | "\u{270d}" |
       "\u{1f3cb}" | "\u{1f3cc}" | "\u{1f574}" | "\u{1f575}" | "\u{1f590}") ~ ("\u{fe0f}" | !"\u{fe0e}")) |
     ("\u{270a}" | "\u{270b}" | "\u{1f385}" | '\u{1f3c2}'..'\u{1f3c4}' | "\u{1f3c7}" | "\u{1f3ca}" | "\u{1f442}" |
      "\u{1f443}" | '\u{1f446}'..'\u{1f450}' | '\u{1f466}'..'\u{1f469}' | "\u{1f46e}" | '\u{1f470}'..'\u{1f478}' |
      "\u{1f47c}" | '\u{1f481}'..'\u{1f483}' | '\u{1f485}'..'\u{1f487}' | "\u{1f4aa}" | "\u{1f57a}" | "\u{1f595}" |
      "\u{1f596}" | '\u{1f645}'..'\u{1f647}' | '\u{1f64b}'..'\u{1f64f}' | "\u{1f6a3}" | '\u{1f6b4}'..'\u{1f6b6}' |
      "\u{1f6c0}" | "\u{1f6cc}" | '\u{1f90c}'..'\u{1f90f}' | '\u{1f918}'..'\u{1f91f}' | "\u{1f926}" |
      '\u{1f930}'..'\u{1f939}' | "\u{1f93c}" | "\u{1f93d}" | "\u{1f93e}" | "\u{1f977}" | "\u{1f9b5}" | "\u{1f9b6}" | "\u{1f9b8}" | "\u{1f9b9}" |
      '\u{1f9bb}'..'\u{1f9bf}' | '\u{1f9cd}'..'\u{1f9cf}' | '\u{1f9d1}'..'\u{1f9dd}' |
      '\u{1fac3}'..'\u{1fac5}' | '\u{1faf0}'..'\u{1faf8}' |
      '\u{1f46b}'..'\u{1f46d}' | "\u{1f46f}")) ~ skin_tone?
}

flags_and_regular_emoji = _{
     "\u{1f3f4}\u{e0067}\u{e0062}\u{e0065}\u{e006e}\u{e0067}\u{e007f}" |
     "\u{1f3f4}\u{e0067}\u{e0062}\u{e0073}\u{e0063}\u{e0074}\u{e007f}" |
     "\u{1f3f4}\u{e0067}\u{e0062}\u{e0077}\u{e006c}\u{e0073}\u{e007f}" |
     ("\u{1f1e6}" ~ ('\u{1f1e8}'..'\u{1f1ec}' | "\u{1f1ee}" | "\u{1f1f1}" | "\u{1f1f2}" | "\u{1f1f4}" |
                     '\u{1f1f6}'..'\u{1f1fa}' | "\u{1f1fc}" | "\u{1f1fd}" | "\u{1f1ff}")) |
     ("\u{1f1e7}" ~ ("\u{1f1e6}" | "\u{1f1e7}" | '\u{1f1e9}'..'\u{1f1ef}' | '\u{1f1f1}'..'\u{1f1f4}' |
                     '\u{1f1f6}'..'\u{1f1f9}' | "\u{1f1fb}" | "\u{1f1fc}" | "\u{1f1fe}" | "\u{1f1ff}")) |
     ("\u{1f1e8}" ~ ("\u{1f1e6}" | "\u{1f1e8}" | "\u{1f1e9}" | '\u{1f1eb}'..'\u{1f1ee}' |
                     '\u{1f1f0}'..'\u{1f1f6}' | "\u{1f1f7}" | '\u{1f1fa}'..'\u{1f1ff}')) |
     ("\u{1f1e9}" ~ ("\u{1f1ea}" | "\u{1f1ec}" | "\u{1f1ef}" | "\u{1f1f0}" | "\u{1f1f2}" | "\u{1f1f4}" | "\u{1f1ff}")) |
     ("\u{1f1ea}" ~ ("\u{1f1e6}" | "\u{1f1e8}" | "\u{1f1ea}" |
                     "\u{1f1ec}" | "\u{1f1ed}" | '\u{1f1f7}'..'\u{1f1fa}')) |
     ("\u{1f1eb}" ~ ('\u{1f1ee}'..'\u{1f1f0}' | "\u{1f1f2}" | "\u{1f1f4}" | "\u{1f1f7}")) |
     ("\u{1f1ec}" ~ ("\u{1f1e6}" | "\u{1f1e7}" | '\u{1f1e9}'..'\u{1f1ee}' | '\u{1f1f1}'..'\u{1f1f3}' |
                     '\u{1f1f5}'..'\u{1f1fa}' | "\u{1f1fc}" | "\u{1f1fe}")) |
     ("\u{1f1ed}" ~ ("\u{1f1f0}" | "\u{1f1f2}" | "\u{1f1f3}" | "\u{1f1f7}" | "\u{1f1f9}" | "\u{1f1fa}" )) |
     ("\u{1f1ee}" ~ ('\u{1f1e8}'..'\u{1f1ea}' | '\u{1f1f1}'..'\u{1f1f4}' | '\u{1f1f6}'..'\u{1f1f9}')) |
     ("\u{1f1ef}" ~ ("\u{1f1ea}" | "\u{1f1f2}" | "\u{1f1f4}" | "\u{1f1f5}")) |
     ("\u{1f1f0}" ~ ("\u{1f1ea}" | '\u{1f1ec}'..'\u{1f1ee}' | "\u{1f1f2}" | "\u{1f1f3}" | "\u{1f1f5}" | "\u{1f1f7}" |
                     "\u{1f1fc}" | "\u{1f1fe}" | "\u{1f1ff}")) |
     ("\u{1f1f1}" ~ ('\u{1f1e6}'..'\u{1f1e8}' | "\u{1f1ee}" | "\u{1f1f0}" | '\u{1f1f7}'..'\u{1f1fb}' | "\u{1f1fe}")) |
     ("\u{1f1f2}" ~ ("\u{1f1e6}" | '\u{1f1e8}'..'\u{1f1ed}' | '\u{1f1f0}'..'\u{1f1ff}')) |
     ("\u{1f1f3}" ~ ("\u{1f1e6}" | "\u{1f1e8}" | '\u{1f1ea}'..'\u{1f1ec}' | "\u{1f1ee}" | "\u{1f1f1}" | "\u{1f1f4}" |
                     "\u{1f1f5}" | "\u{1f1f7}" | "\u{1f1fa}" | "\u{1f1ff}")) |
     "\u{1f1f4}\u{1f1f2}" |
     ("\u{1f1f5}" ~ ("\u{1f1e6}" | '\u{1f1ea}'..'\u{1f1ed}' | '\u{1f1f0}'..'\u{1f1f3}' | '\u{1f1f7}'..'\u{1f1f9}' |
                     "\u{1f1fc}" | "\u{1f1fe}"))|
     "\u{1f1f6}\u{1f1e6}" |
     ("\u{1f1f7}" ~ ("\u{1f1ea}" | "\u{1f1f4}" | "\u{1f1f8}" | "\u{1f1fa}" | "\u{1f1fc}")) |
     ("\u{1f1f8}" ~ ('\u{1f1e6}'..'\u{1f1ea}' | '\u{1f1ec}'..'\u{1f1f4}' | '\u{1f1f7}'..'\u{1f1f9}' | "\u{1f1fb}" | '\u{1f1fd}'..'\u{1f1ff}')) |
     ("\u{1f1f9}" ~ ("\u{1f1e6}" | "\u{1f1e8}" | "\u{1f1e9}" | '\u{1f1eb}'..'\u{1f1ed}' | '\u{1f1ef}'..'\u{1f1f4}' | "\u{1f1f7}" | "\u{1f1f9}" | "\u{1f1fb}" | "\u{1f1fc}" | "\u{1f1ff}")) |
     ("\u{1f1fa}" ~ ("\u{1f1e6}" | "\u{1f1ec}" | "\u{1f1f2}" | "\u{1f1f3}" | "\u{1f1f8}" | "\u{1f1fe}" | "\u{1f1ff}")) |
     ("\u{1f1fb}" ~ ("\u{1f1e6}" | "\u{1f1e8}" | "\u{1f1ea}" | "\u{1f1ec}" | "\u{1f1ee}" | "\u{1f1f3}" | "\u{1f1fa}")) |
     ("\u{1f1fc}" ~ ("\u{1f1eb}" | "\u{1f1f8}")) |
     "\u{1f1fd}\u{01f1f0}" |
     ("\u{1f1fe}" ~ ("\u{1f1ea}" | "\u{1f1f9}")) |
     ("\u{1f1ff}" ~ ("\u{1f1e6}" | "\u{1f1f2}" | "\u{1f1fc}")) |
     ('\u{23e9}'..'\u{23ec}' | "\u{23f0}" | "\u{23f3}" | "\u{267e}" | "\u{26ce}" | "\u{2705}" | "\u{2728}" |
      "\u{274c}" | "\u{274e}" | '\u{2753}'..'\u{2755}' | '\u{2795}'..'\u{2797}' | "\u{27b0}" | "\u{27bf}" |
      "\u{e50a}" | "\u{1f0cf}" | "\u{1f18e}" | '\u{1f191}'..'\u{1f19a}' | '\u{1f1e6}'..'\u{1f1ff}' | "\u{1f201}" |
      '\u{1f232}'..'\u{1f236}' | '\u{1f238}'..'\u{1f23a}' | "\u{1f250}" | "\u{1f251}" | '\u{1f300}'..'\u{1f320}' |
      '\u{1f32d}'..'\u{1f335}' | '\u{1f337}'..'\u{1f37c}' | '\u{1f37e}'..'\u{1f384}' | '\u{1f386}'..'\u{1f393}' |
      '\u{1f3a0}'..'\u{1f3c1}' | "\u{1f3c5}" | "\u{1f3c6}" | "\u{1f3c8}" | "\u{1f3c9}" | '\u{1f3cf}'..'\u{1f3d3}' |
      '\u{1f3e0}'..'\u{1f3f0}' | "\u{1f3f4}" | '\u{1f3f8}'..'\u{1f43e}' | "\u{1f440}" | "\u{1f444}" | "\u{1f445}" |
      '\u{1f451}'..'\u{1f465}' | '\u{1f46a}'..'\u{1f46d}' | "\u{1f46f}" | '\u{1f479}'..'\u{1f47b}' |
      '\u{1f47d}'..'\u{1f480}' | "\u{1f484}" | '\u{1f488}'..'\u{1f4a9}' | '\u{1f4ab}'..'\u{1f4fc}' |
      '\u{1f4ff}'..'\u{1f53d}' | '\u{1f54b}'..'\u{1f54e}' | '\u{1f550}'..'\u{1f567}' | "\u{1f5a4}" |
      '\u{1f5fb}'..'\u{1f644}' | '\u{1f648}'..'\u{1f64a}' | '\u{1f680}'..'\u{1f6a2}' | '\u{1f6a4}'..'\u{1f6b3}' |
      '\u{1f6b7}'..'\u{1f6bf}' | '\u{1f6c1}'..'\u{1f6c5}' | '\u{1f6d0}'..'\u{1f6d2}' | '\u{1f6d5}'..'\u{1f6df}' |
      "\u{1f6eb}" | "\u{1f6ec}" |
      '\u{1f6f4}'..'\u{1f6fc}' | '\u{1f7e0}'..'\u{1f7eb}' | "\u{1f7f0}" |
      '\u{1f90c}'..'\u{1f90f}' | '\u{1f910}'..'\u{1f917}' | "\u{1f91d}" | '\u{1f920}'..'\u{1f925}' |
      '\u{1f927}'..'\u{1f92f}' | "\u{1f93a}" | "\u{1f93c}" | '\u{1f93f}'..'\u{1f945}' | '\u{1f947}'..'\u{1f970}' |
      '\u{1f971}'..'\u{1f972}' | '\u{1f973}'..'\u{1f976}' | '\u{1f977}'..'\u{1f979}' | "\u{1f97a}" | "\u{1f97b}" |
      '\u{1f97c}'..'\u{1f9a2}' | '\u{1f9a3}'..'\u{1f9af}' | '\u{1f9b0}'..'\u{1f9b4}' | "\u{1f9b7}" |
      '\u{1f9ba}'..'\u{1f9bf}' | '\u{1f9c0}'..'\u{1f9c2}' | '\u{1f9c3}'..'\u{1f9cf}' | "\u{1f9d0}" |
      '\u{1f9de}'..'\u{1f9ff}' |
      '\u{1fa70}'..'\u{1fa7c}' | '\u{1fa80}'..'\u{1fac8}' |
      '\u{1facd}'..'\u{1fadc}' | '\u{1fadf}'..'\u{1faef}') |
     "\u{fe0f}"
}

//
// VALIDATION
//
valid_username = { SOI ~ username ~ EOI }
valid_list = { SOI ~ list ~ EOI }
valid_url = { SOI ~ url ~ EOI }

//
// Generate updated parser file with latest TLDs
// bazel build //rust/conformance:update_parser_with_tlds
//
// Output: bazel-bin/rust/conformance/twitter_text_with_tlds.pest
//
// To update the source (when ready):
// cp bazel-bin/rust/conformance/twitter_text_with_tlds.pest rust/parser/src/twitter_text.pest
//
// This rule is generated by tld_gen.py
// See the comments there.
// TLD matching - ASCII and Unicode TLDs are separate (they can't mix scripts)
// Validation happens in Rust via phf lookup
tld_list = _{
    // Punycode TLDs (e.g., xn--p1ai for .—Ä—Ñ) - must come first to match greedily
    tld_punycode |
    // Latin TLDs (ASCII + Latin Extended for TLDs like verm√∂gensberatung)
    tld_latin |
    // Non-Latin Unicode TLDs (IDN) - must be from valid TLD script ranges
    tld_unicode_only
}

// Punycode TLD: starts with "xn--" followed by ASCII alphanumerics and hyphens
tld_punycode = _{
    ^"xn--" ~ tld_punycode_char+
}

tld_punycode_char = _{
    ASCII_ALPHANUMERIC | "-"
}

// Latin TLD: 2+ Latin letters (ASCII or Latin Extended)
tld_latin = _{
    tld_latin_char{2,}
}

tld_latin_char = _{
    // ASCII letters (case insensitive)
    ^"a" | ^"b" | ^"c" | ^"d" | ^"e" | ^"f" | ^"g" | ^"h" | ^"i" | ^"j" | ^"k" | ^"l" | ^"m" |
    ^"n" | ^"o" | ^"p" | ^"q" | ^"r" | ^"s" | ^"t" | ^"u" | ^"v" | ^"w" | ^"x" | ^"y" | ^"z" |
    // Latin-1 Supplement (√†√°√¢√£√§√•√¶√ß√®√©√™√´√¨√≠√Æ√Ø√∞√±√≤√≥√¥√µ√∂√∏√π√∫√ª√º√Ω√æ√ø etc.)
    '\u{00c0}'..'\u{00ff}' |
    // Latin Extended-A
    '\u{0100}'..'\u{017f}' |
    // Latin Extended-B
    '\u{0180}'..'\u{024f}'
}

// Unicode TLD: 2+ characters from valid IDN TLD scripts only
tld_unicode_only = _{ tld_unicode_char{2,} }

tld_unicode_char = _{
    // Greek
    '\u{0370}'..'\u{03ff}' |
    // Cyrillic
    '\u{0400}'..'\u{04ff}' |
    // Armenian
    '\u{0530}'..'\u{058f}' |
    // Hebrew
    '\u{0590}'..'\u{05ff}' |
    // Arabic
    '\u{0600}'..'\u{06ff}' | '\u{0750}'..'\u{077f}' |
    // Indic scripts (Devanagari, Bengali, Gurmukhi, Gujarati, Oriya, Tamil, Telugu, Kannada, Malayalam, Sinhala)
    '\u{0900}'..'\u{0dff}' |
    // Thai
    '\u{0e00}'..'\u{0e7f}' |
    // Lao
    '\u{0e80}'..'\u{0eff}' |
    // Georgian
    '\u{10a0}'..'\u{10ff}' |
    // Japanese Hiragana and Katakana
    '\u{3040}'..'\u{30ff}' |
    // CJK Ideographs (Chinese, Japanese Kanji, Korean Hanja)
    '\u{4e00}'..'\u{9fff}' |
    // Korean Hangul
    '\u{ac00}'..'\u{d7af}' |
    // Latin Extended (for things like √∂ in verm√∂gensberater)
    '\u{00c0}'..'\u{00ff}' | '\u{0100}'..'\u{017f}'
}
