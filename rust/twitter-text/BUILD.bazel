package(default_visibility = ["//visibility:public"])

load("@rules_rust//rust:defs.bzl", "rust_library", "rust_shared_library", "rust_static_library", "rust_test")
load("//rust_bindings/3rdparty/cxxbridge:rust_cxx_bridge.bzl", "rust_cxx_bridge")

filegroup(
    name = "twitter_text_ffi",
    srcs = ["src/ffi.rs"],
)

twitter_text_srcs = [
    "src/lib.rs",
    "src/autolinker.rs",
    "src/entity.rs",
    "src/extractor.rs",
    "src/hit_highlighter.rs",
    "src/validator.rs",
    "src/ffi.rs",
]

twitter_text_deps = [
    "//rust/config:twitter_text_config",
    "//rust/parser:twitter_text_parser",
    "//rust_bindings/3rdparty/crates:cxx",   # Rust side of CXX
    "//rust_bindings/3rdparty/crates:idna",
    "//rust_bindings/3rdparty/crates:pest",
    "//rust_bindings/3rdparty/crates:unicode-normalization",
]

# 1) Core Rust crate, no cxx in the public API
rust_library(
    name = "twitter_text_rlib",
    crate_name = "twitter_text",
    crate_root = "src/lib.rs",
    srcs = twitter_text_srcs,
    deps = twitter_text_deps,
    edition = "2021",
)

# 2) Static library with FFI, built from the same sources but with ffi feature
rust_static_library(
    name = "twitter_text",
    crate_root = "src/lib.rs",
    srcs = twitter_text_srcs,
    deps = twitter_text_deps,
    crate_features = ["ffi"],
    edition = "2021",
)

genrule(
    name = "cxxbridge",
    outs = ["cxx.h"],
    cmd = "$(location @cxxbridge-cmd//:cxxbridge-cmd) --header > $(location cxx.h)",
    tools = ["@cxxbridge-cmd//:cxxbridge-cmd"],
)

genrule(
    name = "twitter_text_h",
    srcs = [
        "//rust/twitter-text:twitter_text_ffi",
    ],
    outs = ["twitter-text.h"],
    cmd = "$(location @cxxbridge-cmd//:cxxbridge-cmd) --header $(location //rust/twitter-text:twitter_text_ffi) | sed 's/\\$$cxxbridge1\\$$0\\$$/\\$$cxxbridge1\\$$189\\$$/g' > $(location twitter-text.h)",
    tools = ["@cxxbridge-cmd//:cxxbridge-cmd"],
)

genrule(
    name = "twitter_text_cpp",
    srcs = [
        ":twitter_text",
        "//rust/twitter-text:twitter_text_ffi",
    ],
    outs = ["twitter-text.cpp"],
    cmd = "$(location @cxxbridge-cmd//:cxxbridge-cmd) $(location //rust/twitter-text:twitter_text_ffi) | sed 's/\\$$cxxbridge1\\$$0\\$$/\\$$cxxbridge1\\$$189\\$$/g' > $(location twitter-text.cpp)",
    tools = ["@cxxbridge-cmd//:cxxbridge-cmd"],
)

cc_library(
    name = "twitter_text_wrapper",
    hdrs = [
        ":cxxbridge",
        ":twitter_text_h",
    ],
    srcs = [
        ":twitter_text_cpp",
    ],
    deps = [
        ":twitter_text",
        "//rust_bindings/3rdparty/crates:cxx_cc"
    ],
    copts = [
        "-Iexternal/rust_bindings/3rdparty/crates/cxx/include",
    ],
)

rust_test(
    name = "twitter_text_test",
    crate = ":twitter_text_rlib",
)
