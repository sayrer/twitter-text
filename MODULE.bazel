module(
    name = "twitter_text_rs",
    version = "0.3.0",
)

# Check that Ruby 3.4.1+ is on PATH (required for rules_ruby with version="system")
ruby_version_check = use_repo_rule("//tools:ruby_version_check.bzl", "ruby_version_check")

ruby_version_check(name = "ruby_version_check")

###############################################################################
# Bazel Dependencies
###############################################################################

bazel_dep(name = "bazel_skylib", version = "1.8.2")
bazel_dep(name = "googletest", version = "1.17.0.bcr.2")
bazel_dep(name = "rules_rust", version = "0.67.0")
bazel_dep(name = "rules_rust_wasm_bindgen", version = "0.67.0")

# apple_support MUST come before rules_cc for proper toolchain selection on macOS
bazel_dep(name = "apple_support", version = "1.24.2")
bazel_dep(name = "rules_cc", version = "0.2.14")
bazel_dep(name = "cxx.rs", version = "1.0.188")
bazel_dep(name = "rules_ruby", version = "0.21.1")
bazel_dep(name = "rules_apple", version = "4.3.2")

# Apple CC toolchain for macOS (required for swift_test with XCTest)
apple_cc_configure = use_extension(
    "@apple_support//crosstool:setup.bzl",
    "apple_cc_configure_extension",
)
use_repo(apple_cc_configure, "local_config_apple_cc", "local_config_apple_cc_toolchains")

bazel_dep(name = "rules_java", version = "8.14.0")
bazel_dep(name = "rules_jvm_external", version = "6.9")
bazel_dep(name = "rules_python", version = "1.7.0")
bazel_dep(name = "rules_swift", version = "3.3.0")

# Fix a bug in rules_swift with hermetic toolchains
single_version_override(
    module_name = "rules_swift",
    patch_strip = 1,
    patches = ["//patches:rules_swift.patch"],
    version = "3.3.0",
)

bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "yams", version = "6.2.0")
single_version_override(
    module_name = "yams",
    patch_strip = 1,
    patches = ["//patches:yams.patch"],
    version = "6.2.0",
)

bazel_dep(name = "aspect_rules_js", version = "2.3.7")
bazel_dep(name = "rules_nodejs", version = "6.3.4")

###############################################################################
# Java Toolchains and Maven Dependencies
###############################################################################

java_toolchains = use_extension("@rules_java//java:extensions.bzl", "toolchains")

maven = use_extension("@rules_jvm_external//:extensions.bzl", "maven")
maven.install(
    artifacts = [
        "junit:junit:4.13.2",
        "com.fasterxml.jackson.core:jackson-databind:2.9.10.8",
        "com.fasterxml.jackson.core:jackson-core:2.9.10",
        "com.fasterxml.jackson.core:jackson-annotations:2.9.10",
        "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.10",
        "com.google.code.findbugs:jsr305:3.0.2",
    ],
    repositories = [
        "https://repo1.maven.org/maven2",
    ],
)
use_repo(maven, "maven")

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

###############################################################################
# C Libraries for Ruby
###############################################################################

http_archive(
    name = "libyaml",
    build_file_content = """
cc_library(
    name = "yaml",
    srcs = glob(["src/*.c"]),
    hdrs = glob(["include/**/*.h"]),
    includes = ["include"],
    visibility = ["//visibility:public"],
)
    """,
    sha256 = "c642ae9b75fee120b2d96c712538bd2cf283228d2337df2cf2988e3c02678ef4",
    strip_prefix = "libyaml-0.2.5",
    urls = ["https://github.com/yaml/libyaml/archive/refs/tags/0.2.5.tar.gz"],
)

http_archive(
    name = "zlib",
    build_file_content = """
cc_library(
    name = "zlib",
    srcs = glob(["*.c"]),
    hdrs = glob(["*.h"]),
    includes = ["."],
    visibility = ["//visibility:public"],
)
    """,
    sha256 = "9a93b2b7dfdac77ceba5a558a580e74667dd6fede4585b91eefb60f03b72df23",
    strip_prefix = "zlib-1.3.1",
    urls = ["https://github.com/madler/zlib/releases/download/v1.3.1/zlib-1.3.1.tar.gz"],
)

http_archive(
    name = "readline",
    build_file_content = """
cc_library(
    name = "readline",
    srcs = glob(["*.c"]),
    hdrs = glob(["*.h"]),
    includes = ["."],
    visibility = ["//visibility:public"],
    linkopts = ["-lncurses"],
)
    """,
    sha256 = "f8ceb4ee131e3232226a17f51b164afc46cd0b9e6cef344be87c65962cb82b02",
    strip_prefix = "readline-8.2",
    urls = ["https://ftp.gnu.org/gnu/readline/readline-8.2.tar.gz"],
)

###############################################################################
# Swift Toolchains
###############################################################################

http_archive(
    name = "swift_linux_x86_64",
    build_file_content = """
package(default_visibility = ["//visibility:public"])

filegroup(
    name = "usr",
    srcs = glob(["usr/**/*"]),
)

filegroup(
    name = "runtime_libs",
    srcs = glob(["usr/lib/swift/linux/*.so"]),
)

genrule(
    name = "version_file",
    outs = ["swift_version.txt"],
    cmd = "echo '6.0.3' > $@",
    visibility = ["//visibility:public"],
)

exports_files(glob(["usr/**/*"]))
""",
    sha256 = "09d0a53fbddf2878673dd12c1504e7143c788f195caec9f2329740946eba525c",
    strip_prefix = "swift-6.0.3-RELEASE-ubuntu22.04",
    urls = ["https://download.swift.org/swift-6.0.3-release/ubuntu2204/swift-6.0.3-RELEASE/swift-6.0.3-RELEASE-ubuntu22.04.tar.gz"],
)

http_archive(
    name = "swift_linux_aarch64",
    build_file_content = """
package(default_visibility = ["//visibility:public"])

filegroup(
    name = "usr",
    srcs = glob(["usr/**/*"]),
)

filegroup(
    name = "runtime_libs",
    srcs = glob(["usr/lib/swift/linux/*.so"]),
)

genrule(
    name = "version_file",
    outs = ["swift_version.txt"],
    cmd = "echo '6.0.3' > $@",
    visibility = ["//visibility:public"],
)

exports_files(glob(["usr/**/*"]))
""",
    sha256 = "52fb0b8feb987c99a7a8217b0d71f62f97c0252b1a2b0058c08c0b7374cd2d66",
    strip_prefix = "swift-6.0.3-RELEASE-ubuntu22.04-aarch64",
    urls = ["https://download.swift.org/swift-6.0.3-release/ubuntu2204-aarch64/swift-6.0.3-RELEASE/swift-6.0.3-RELEASE-ubuntu22.04-aarch64.tar.gz"],
)

register_toolchains("//tools/swift:hermetic_swift_linux_x86_64_toolchain")

###############################################################################
# Hermetic LLVM Toolchain
###############################################################################

http_archive(
    name = "linux_sysroot",
    build_file_content = """
filegroup(
    name = "sysroot",
    srcs = glob(["**"]),
    visibility = ["//visibility:public"],
)
""",
    sha256 = "e656b9c486a0e5a0aab372bcc8d7c4fc892dddd8e62ec2fbc5222155280a960e",
    urls = ["https://commondatastorage.googleapis.com/chrome-linux-sysroot/toolchain/98e34ae4d2db673096fa8020e301defee6b877dd/debian_bullseye_amd64_sysroot.tar.xz"],
)

bazel_dep(name = "toolchains_llvm", version = "1.6.0")

llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm")
llvm.toolchain(
    cxx_builtin_include_directories = {
        "": [
            "/usr/include",
            "/usr/local/include",
        ],
    },
    llvm_version = "17.0.6",
    sha256 = {"": "884ee67d647d77e58740c1e645649e29ae9e8a6fe87c1376be0f3a30f3cc9ab3"},
    strip_prefix = {
        "": "clang+llvm-17.0.6-x86_64-linux-gnu-ubuntu-22.04",
    },
    urls = {
        "": ["https://github.com/llvm/llvm-project/releases/download/llvmorg-17.0.6/clang+llvm-17.0.6-x86_64-linux-gnu-ubuntu-22.04.tar.xz"],
    },
)
llvm.sysroot(
    label = "@linux_sysroot//:sysroot",
    targets = ["linux-x86_64"],
)
use_repo(llvm, "llvm_toolchain")
use_repo(llvm, "llvm_toolchain_llvm")

# Register Apple CC toolchain first so it takes precedence on macOS
register_toolchains("@local_config_apple_cc_toolchains//:all")

register_toolchains("@llvm_toolchain//:all")

###############################################################################
# Rust Toolchains
###############################################################################

RUST_EDITION = "2021"

RUST_VERSION = "1.91.1"

rust_ext = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust_ext.toolchain(
    edition = RUST_EDITION,
    extra_target_triples = ["wasm32-unknown-unknown"],
    versions = [RUST_VERSION],
)
use_repo(rust_ext, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

###############################################################################
# Python Toolchain (for PyO3)
###############################################################################

python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(
    python_version = "3.12",
)
use_repo(python, "python_3_12", "python_versions")

register_toolchains("@python_3_12//:all")

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")
pip.parse(
    hub_name = "tld_gen_deps",
    python_version = "3.12",
    requirements_lock = "//rust/conformance:tests/tld_gen_requirements.txt",
)
pip.parse(
    hub_name = "rust_python_test_deps",
    python_version = "3.12",
    requirements_lock = "//rust/python-bindings/test:requirements.txt",
)
use_repo(pip, "rust_python_test_deps", "tld_gen_deps")

###############################################################################
# Ruby Toolchain (for Magnus)
###############################################################################

ruby = use_extension("@rules_ruby//ruby:extensions.bzl", "ruby")
ruby.toolchain(
    name = "ruby",
    version = "system",
)
ruby.bundle_fetch(
    name = "bundle",
    gem_checksums = {
        "diff-lcs-1.4.4": "bf3a658875f70c1467fe7a3b302b795f074c84b28db6e4a2bd6b1ad6d12a2255",
        "rspec-3.7.0": "0174cfbed780e42aa181227af623e2ae37511f20a2fdfec48b54f6cf4d7a6404",
        "rspec-core-3.7.1": "526e0b2d22feae638eb99bd746bbb25b18a4869a8f6ce4e8a87dabd1ab5f727f",
        "rspec-expectations-3.7.0": "7e571848a5cbdb1661187d04e5c1f29287ec80fcb5a395f9994836892a3780bb",
        "rspec-mocks-3.7.0": "23f7f0039e17f2841edfb51d678ac9e06c056903a7908db967f5618887f2022c",
        "rspec-support-3.7.1": "4152975a068756076f10c97317e9d2baf10028f3d25c0cb902945569560783da",
        "yaml-0.1.0": "c10a32e8d0ab40f97d965c9a9cf35e2106c7edf534311af19bfe1053c81680c9",
    },
    gemfile = "//:Gemfile",
    gemfile_lock = "//:Gemfile.lock",
)
use_repo(ruby, "bundle", "ruby", "ruby_toolchains")

register_toolchains("@ruby_toolchains//:all")

###############################################################################
# Rust Crates
###############################################################################

deps = use_extension("//:extensions.bzl", "rust_deps")
use_repo(
    deps,
    "twitter_text",
    "twitter_text__clap-4.5.53",
    "twitter_text__cxx-1.0.190",
    "twitter_text__emojis-0.8.0",
    "twitter_text__gungraun-0.17.0",
    "twitter_text__idna-1.1.0",
    "twitter_text__js-sys-0.3.77",
    "twitter_text__lazy_static-1.5.0",
    "twitter_text__libc-0.2.178",
    "twitter_text__log-0.4.28",
    "twitter_text__magnus-0.8.2",
    "twitter_text__memchr-2.7.6",
    "twitter_text__nom-7.1.3",
    "twitter_text__pest-2.8.4",
    "twitter_text__pest_derive-2.8.4",
    "twitter_text__phf-0.11.3",
    "twitter_text__proc-macro-error-1.0.4",
    "twitter_text__pyo3-0.20.3",
    "twitter_text__pyo3-macros-0.20.3",
    "twitter_text__serde-1.0.228",
    "twitter_text__serde_derive-1.0.228",
    "twitter_text__serde_json-1.0.145",
    "twitter_text__serde_yaml_ng-0.10.0",
    "twitter_text__unicode-normalization-0.1.25",
    "twitter_text__unicode_categories-0.1.1",
    "twitter_text__wasm-bindgen-0.2.100",
)

###############################################################################
# cxxbridge-cmd (codegen tool for cxx.rs)
###############################################################################

CXX_BRIDGE_VERSION = "1.0.190"

http_archive(
    name = "cxxbridge-cmd",
    build_file_content = """
load("@rules_rust//rust:defs.bzl", "rust_binary")
load("@cxxbridge_cmd_deps//:defs.bzl", "aliases", "all_crate_deps")

rust_binary(
    name = "cxxbridge-cmd",
    srcs = glob(["src/**/*.rs"]),
    aliases = aliases(),
    compile_data = glob(["src/gen/**/*.h"]),
    edition = "2021",
    visibility = ["//visibility:public"],
    deps = all_crate_deps(
        normal = True,
    ),
    version = "%s",
)
    """ % CXX_BRIDGE_VERSION,
    sha256 = "b1f29a879d35f7906e3c9b77d7a1005a6a0787d330c09dfe4ffb5f617728cb44",
    strip_prefix = "cxxbridge-cmd-" + CXX_BRIDGE_VERSION,
    type = "tar.gz",
    urls = ["https://static.crates.io/crates/cxxbridge-cmd/cxxbridge-cmd-" + CXX_BRIDGE_VERSION + ".crate"],
)

cxxbridge_cmd_deps = use_extension("@rules_rust//crate_universe:extensions.bzl", "crate")
cxxbridge_cmd_deps.splicing_config(
    repositories = ["cxxbridge_cmd_deps"],
    resolver_version = "2",
)
cxxbridge_cmd_deps.from_cargo(
    name = "cxxbridge_cmd_deps",
    cargo_lockfile = "//3rdparty/cxxbridge:Cargo.lock",
    manifests = ["@cxxbridge-cmd//:Cargo.toml"],
)
use_repo(
    cxxbridge_cmd_deps,
    "cxxbridge_cmd_deps",
)

###############################################################################
# C++ (yaml-cpp)
###############################################################################

http_archive(
    name = "com_github_jbeder_yaml_cpp",
    sha256 = "fbe74bbdcee21d656715688706da3c8becfd946d92cd44705cc6098bb23b3a16",
    strip_prefix = "yaml-cpp-0.8.0",
    urls = [
        "https://github.com/jbeder/yaml-cpp/archive/refs/tags/0.8.0.tar.gz",
    ],
)

###############################################################################
# Java (JDK 23 and jextract)
###############################################################################

jdk23_ext = use_extension("//tools:jdk23_and_jextract.bzl", "jdk23_ext")

jdk23_ext.add_jdk(
    name = "jdk23_macos_arm64",
    cpu = "arm64",
    os = "macos",
    sha256 = "749993e751f085c7ae713140066a90800075e4aeedfac50a5ed0c5457131c5a0",
    strip_prefix = "jdk-23.0.2+7",
    urls = [
        "https://github.com/adoptium/temurin23-binaries/releases/download/jdk-23.0.2%2B7/OpenJDK23U-jdk_aarch64_mac_hotspot_23.0.2_7.tar.gz",
    ],
)
jdk23_ext.add_jdk(
    name = "jdk23_linux_x86_64",
    cpu = "x86_64",
    os = "linux",
    sha256 = "870ac8c05c6fe563e7a3878a47d0234b83c050e83651d2c47e8b822ec74512dd",
    strip_prefix = "jdk-23.0.2+7",
    urls = [
        "https://github.com/adoptium/temurin23-binaries/releases/download/jdk-23.0.2%2B7/OpenJDK23U-jdk_x64_linux_hotspot_23.0.2_7.tar.gz",
    ],
)
jdk23_ext.add_jdk(
    name = "jdk23_linux_arm64",
    cpu = "arm64",
    os = "linux",
    sha256 = "2ff2fa69a415364948ec383b7d33a78e3c3a8b74e958c630c1099a182fe4df19",
    strip_prefix = "jdk-23.0.2+7",
    urls = [
        "https://github.com/adoptium/temurin23-binaries/releases/download/jdk-23.0.2%2B7/OpenJDK23U-jdk_aarch64_linux_hotspot_23.0.2_7.tar.gz",
    ],
)

jdk23_ext.add_jextract(
    name = "jextract_macos_arm64",
    cpu = "arm64",
    os = "macos",
    sha256 = "",
    strip_prefix = "jextract-25",
    urls = [
        "https://download.java.net/java/early_access/jextract/25/2/openjdk-25-jextract+2-4_macos-aarch64_bin.tar.gz",
    ],
)
jdk23_ext.add_jextract(
    name = "jextract_linux_x86_64",
    cpu = "x86_64",
    os = "linux",
    sha256 = "",
    strip_prefix = "jextract-25",
    urls = [
        "https://download.java.net/java/early_access/jextract/25/2/openjdk-25-jextract+2-4_linux-x64_bin.tar.gz",
    ],
)
jdk23_ext.add_jextract(
    name = "jextract_linux_arm64",
    cpu = "arm64",
    os = "linux",
    sha256 = "",
    strip_prefix = "jextract-25",
    urls = [
        "https://download.java.net/java/early_access/jextract/25/2/openjdk-25-jextract+2-4_linux-aarch64_bin.tar.gz",
    ],
)
use_repo(
    jdk23_ext,
    "jdk23_linux_arm64_repo",
    "jdk23_linux_x86_64_repo",
    "jdk23_macos_arm64_repo",
    "jdk23_toolchains",
    "jextract_linux_arm64_repo",
    "jextract_linux_x86_64_repo",
    "jextract_macos_arm64_repo",
)

register_toolchains("@jdk23_toolchains//:all")

###############################################################################
# Node.js (for WASM tests)
###############################################################################

node = use_extension("@rules_nodejs//nodejs:extensions.bzl", "node")
node.toolchain(
    name = "nodejs",
    node_version = "20.11.1",
)
use_repo(node, "nodejs", "nodejs_toolchains")

register_toolchains("@nodejs_toolchains//:all")

npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm")
npm.npm_translate_lock(
    name = "npm",
    pnpm_lock = "//:pnpm-lock.yaml",
)
use_repo(npm, "npm")
