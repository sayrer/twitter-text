module(
    name = "twitter_text_rs",
    version = "0.3.0",
)

# -- bazel_dep definitions -- #
bazel_dep(name = "bazel_skylib", version = "1.8.2")
bazel_dep(name = "googletest", version = "1.17.0.bcr.2")
bazel_dep(name = "rules_rust", version = "0.67.0")
bazel_dep(name = "rules_cc", version = "0.2.14")
bazel_dep(name = "cxx.rs", version = "1.0.188")
bazel_dep(name = "rules_ruby", version = "0.21.1")
bazel_dep(name = "rules_apple", version = "4.3.2")
bazel_dep(name = "rules_java", version = "8.14.0")
bazel_dep(name = "rules_python", version = "1.7.0")
bazel_dep(name = "rules_swift", version = "3.3.0")
bazel_dep(name = "platforms", version = "1.0.0")

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

###############################################################################
# Hermetic LLVM wrapper
###############################################################################

bazel_dep(name = "toolchains_llvm", version = "1.5.0")

# Configure and register the toolchain.
llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm")
llvm.toolchain(
    llvm_version = "16.0.0",
)
use_repo(llvm, "llvm_toolchain")
# use_repo(llvm, "llvm_toolchain_llvm") # if you depend on specific tools in scripts

register_toolchains("@llvm_toolchain//:all")

###############################################################################
# Rust toolchains from rules_rust (0.67.0)
###############################################################################

RUST_EDITION = "2021"

RUST_VERSION = "1.91.1"  # as you had

rust_ext = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust_ext.toolchain(
    edition = RUST_EDITION,
    versions = [RUST_VERSION],
)
use_repo(rust_ext, "rust_toolchains")

# This registers the standard rules_rust toolchains (rustc, cargo, etc.)
register_toolchains("@rust_toolchains//:all")

###############################################################################
# R U S T  C R A T E S
###############################################################################
deps = use_extension("//:extensions.bzl", "rust_deps")
use_repo(
    deps,
    "twitter_text",
    "twitter_text__clap-4.5.53",
    "twitter_text__cxx-1.0.189",
    "twitter_text__idna-1.1.0",
    "twitter_text__lazy_static-1.5.0",
    "twitter_text__log-0.4.28",
    "twitter_text__magnus-0.8.2",
    "twitter_text__pest-2.8.4",
    "twitter_text__pest_derive-2.8.4",
    "twitter_text__proc-macro-error-1.0.4",
    "twitter_text__serde-1.0.228",
    "twitter_text__serde_derive-1.0.228",
    "twitter_text__serde_json-1.0.145",
    "twitter_text__serde_yaml_ng-0.10.0",
    "twitter_text__unicode-normalization-0.1.25",
)

#
# The codegen tool needed by cxx.
#
http_archive(
    name = "cxxbridge-cmd",
    build_file_content = """
load("@rules_rust//rust:defs.bzl", "rust_binary")
load("@cxxbridge_cmd_deps//:defs.bzl", "aliases", "all_crate_deps")

rust_binary(
    name = "cxxbridge-cmd",
    srcs = glob(["src/**/*.rs"]),
    aliases = aliases(),
    compile_data = glob(["src/gen/**/*.h"]),
    edition = "2021",
    visibility = ["//visibility:public"],
    deps = all_crate_deps(
        normal = True,
    ),
)
    """,
    sha256 = "6a368ed4a0fd83ebd3f2808613842d942a409c41cc24cd9d83f1696a00d78afe",
    strip_prefix = "cxxbridge-cmd-1.0.189",
    type = "tar.gz",
    urls = ["https://static.crates.io/crates/cxxbridge-cmd/cxxbridge-cmd-1.0.189.crate"],
)

cxxbridge_cmd_deps = use_extension("@rules_rust//crate_universe:extensions.bzl", "crate")
cxxbridge_cmd_deps.splicing_config(
    repositories = ["cxxbridge_cmd_deps"],
    resolver_version = "2",
)
cxxbridge_cmd_deps.from_cargo(
    name = "cxxbridge_cmd_deps",
    cargo_lockfile = "//rust_bindings/3rdparty/cxxbridge:Cargo.lock",
    manifests = ["@cxxbridge-cmd//:Cargo.toml"],
)
use_repo(
    cxxbridge_cmd_deps,
    "cxxbridge_cmd_deps",
)

###############################################################################
# C++
###############################################################################

http_archive(
    name = "com_github_jbeder_yaml_cpp",
    sha256 = "fbe74bbdcee21d656715688706da3c8becfd946d92cd44705cc6098bb23b3a16",
    strip_prefix = "yaml-cpp-0.8.0",
    urls = [
        "https://github.com/jbeder/yaml-cpp/archive/refs/tags/0.8.0.tar.gz",
    ],
)
